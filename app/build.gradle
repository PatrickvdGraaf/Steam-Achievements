apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

android {
    def STRING = 'String'
    def API_URL = 'API_URL'
    def DB_NAME = 'DB_NAME'
    def STEAM_API_KEY = 'STEAM_API_KEY'
    def TEST_USER_ID = 'TEST_USER_ID'

    androidExtensions {
        experimental = true
    }

    buildToolsVersion '28.0.3'

    buildTypes {
        debug {
            ext.enableCrashlytics = false
            aaptOptions.cruncherEnabled = false
            debuggable true
            testCoverageEnabled !project.hasProperty('android.injected.invoked.from.ide')
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileSdkVersion 29

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    dataBinding {
        enabled = true
    }

    defaultConfig {
        applicationId "com.crepetete.steamachievements"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "0.0.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField STRING, API_URL, '"https://api.steampowered.com"'
        buildConfigField STRING, DB_NAME, '"players.db"'

        /* Define the following values in your local.properties. */
        Properties properties = new Properties()
        properties.load(project.rootProject.file("local.properties").newDataInputStream())
        def steam_api_key = properties.getProperty("steam_api_key", "")
        def test_user_id = properties.getProperty("test_user_id", "")

        /* An API key can be obtained from https://steamcommunity.com/dev/apikey. */
        buildConfigField STRING, STEAM_API_KEY, steam_api_key

        /* It's recommended to use your own steam ID, which can be found in your profile URL,
         or can be found with tools like https://steamidfinder.com. */
        buildConfigField STRING, TEST_USER_ID, test_user_id
    }

    // Specifies one flavor dimension.
    flavorDimensions "version"
    lintOptions {
        lintConfig rootProject.file('lint.xml')
    }

    productFlavors {
        // When building a variant that uses this flavor, the following configurations
        // override those in the defaultConfig block.
        dev {
            applicationIdSuffix '.dev'
            versionNameSuffix "-dev"

            // Only use this flag on builds you don't proguard or upload to beta-by-crashlytics
            ext.alwaysUpdateBuildId = false

            // Assigns this product flavor to the "version" flavor dimension.
            // This property is optional if you are using only one dimension.
            dimension "version"

            // The following configuration limits the "dev" flavor to using
            // English string resources and xxhdpi screen-density resources.
            resConfigs "en", "xxhdpi"
        }

        prod {
            // If you've configured the defaultConfig block for the release version of
            // your app, you can leave this block empty and Gradle uses configurations in
            // the defaultConfig block instead. You still need to create this flavor.
            // Otherwise, all variants use the "dev" flavor configurations.

            dimension "version"
        }
    }

    sourceSets {
        androidTest.java.srcDirs += "src/test-common/java"
        test.java.srcDirs += "src/test-common/java"
    }
}

dependencies {
    def support_version = '1.0.0'
    def lifecycle_version = "2.1.0"
    def retrofit_version = '2.6.0'
    def okhttp_version = '4.2.0'
    def dagger_version = '2.21'
    def glide_version = '4.9.0-SNAPSHOT'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Android
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.fragment:fragment-ktx:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.palette:palette:$support_version"
    implementation "androidx.recyclerview:recyclerview:$support_version"
    implementation "androidx.cardview:cardview:$support_version"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'
    implementation 'com.google.android.material:material:1.0.0'

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.1'

    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version" // ViewModel and LiveData
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.2.0-alpha05' // liveData
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0-alpha05' // ViewModelScope

    // Logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    //Retrofit 2
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    // Dagger 2
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    //Rx
    implementation 'io.reactivex.rxjava2:rxjava:2.1.14'
    /* RxAndroid adds android specific bindings for RxJava,
       Specifically AndroidSchedulers.mainThread() which provides a Scheduler that schedules
       on main thread and can be used to switch between threads in Android. */
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'

    // Room
    implementation "androidx.room:room-runtime:2.2.0-rc01"
    implementation 'androidx.room:room-ktx:2.2.0-rc01'
    kapt "androidx.room:room-compiler:2.2.0-rc01"

    // Glide Image Loading
    // TODO move to Coil; https://tech.instacart.com/introducing-coil-kotlin-first-image-loading-on-android-f0fdc7a2a99e
    implementation "com.github.bumptech.glide:glide:$glide_version"
    implementation "com.github.bumptech.glide:okhttp-integration:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    // Firebase
    implementation 'com.google.firebase:firebase-core:16.0.6'
    implementation 'com.google.firebase:firebase-perf:16.2.3'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.8'

    // Graph Library; http://www.android-graphview.org/download-getting-started/
    implementation 'com.jjoe64:graphview:4.2.2'

    // Search Bar Animation; https://github.com/UdiOshi85/libSearchToolbar
    implementation 'com.github.udioshi85:libSearchToolbar:1.3.0'

    // Testing
    testImplementation 'junit:junit:4.12'
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.8.1'
    testImplementation 'androidx.arch.core:core-testing:2.0.0'
    testImplementation 'org.mockito:mockito-core:2.22.0'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.1.1'
    androidTestImplementation 'androidx.appcompat:appcompat:1.0.2'
    androidTestImplementation 'androidx.recyclerview:recyclerview:1.0.0'
    androidTestImplementation 'androidx.cardview:cardview:1.0.0'
    androidTestImplementation 'com.google.android.material:material:1.0.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.1.1'
    androidTestImplementation 'androidx.arch.core:core-testing:2.0.0'
    androidTestImplementation 'org.mockito:mockito-core:2.22.0'
    androidTestImplementation 'org.mockito:mockito-android:2.22.0'
}

apply plugin: 'com.google.gms.google-services'
